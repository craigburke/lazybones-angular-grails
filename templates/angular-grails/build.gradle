import org.grails.gradle.plugin.tasks.GrailsTask

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.grails:grails-gradle-plugin:2.1.2'
		classpath 'com.moowork.gradle:gradle-node-plugin:0.6'
	}
}

project.ext {
	warFile = file("target/${project.warName}")
	jasmineDependencies = ['karma', 'karma-jasmine@2_0', 'karma-phantomjs-launcher']
	karmaExec = file('node_modules/karma/bin/karma')
	karmaConfig = 'test/js/karma.conf.js'
}

apply plugin: 'grails'
apply plugin: 'com.moowork.node'

apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
	grails.central()
    flatDir {
        dirs 'lib'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

grails {
	grailsVersion = '2.5.0'
	groovyVersion = '2.4.3'
}

node {
	version = '0.11.13'
	distBaseUrl = 'http://nodejs.org/dist'
	download = true
	workDir = file("${project.buildDir}/nodejs")
}

clean {
	delete warFile
	delete file('node_modules')
}

war {
	outputFile = warFile
	env = 'production'
}

task debug(type: GrailsTask) {
        command = 'run-app'
        jvmOptions {
                jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,address=5005'
        }
}

tasks.withType(GrailsTask) {
	jvmOptions {
		minHeapSize = '1g'
		maxHeapSize = '1g'
		jvmArgs "-XX:MaxPermSize=512m" 
	}
}

task setupPhantomJS(type: NpmTask) {
	outputs.dir file('node_modules')
	args = ['install', 'phantomjs', '--loglevel', 'error']
}

task setupJasmine(type: NpmTask) {
	outputs.dir file('node_modules')
	args = ['install'] + jasmineDependencies + ['--loglevel', 'error']
}

task jasmineRun(type: NodeTask, dependsOn: ['setupPhantomJS', 'setupJasmine']) {
	script = karmaExec
	args = ['start', karmaConfig, '--single-run']
}

task jasmineWatch(type: NodeTask, dependsOn: 'setupJasmine') {
        script = karmaExec
        args = ['start', karmaConfig]
}

test.dependsOn jasmineRun

dependencies {
	springloaded 'org.springframework:springloaded:1.2.3.RELEASE@jar'
	
  	String gebVersion = '0.9.2'
	String seleniumVersion = '2.43.1'

	test "org.grails:grails-datastore-test-support:1.0.2-grails-2.4"
	test "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
	test "org.gebish:geb-spock:$gebVersion"
	
    test("com.github.detro.ghostdriver:phantomjsdriver:1.1.0") {
		transitive = false
	}

	def plugins = [
		[name: 'tomcat', version: '7.0.55.2', scope: 'bootstrap'],
		[name: 'scaffolding', version: '2.1.2'],
		[name: 'cache', version: '1.1.8'],
		[name: 'asset-pipeline', version: '2.2.0'],
		[name: 'angular-template-asset-pipeline', version: '2.1.0'],
		[name: 'angular-annotate-asset-pipeline', version: '2.1.0'],
		[name: 'geb', version: gebVersion, scope: 'test']
	]

	plugins.each { plugin ->
		String scope = plugin.scope ?: 'compile'
		owner."$scope" "org.grails.plugins:${plugin.name}:${plugin.version}"
	}
		
	runtime('org.grails.plugins:hibernate4:4.3.8.1') {
    		exclude module: 'xml-apis'
	}
	
}